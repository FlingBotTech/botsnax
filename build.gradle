plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "edu.wpi.first.GradleRIO" version "2025.2.1"
}

def javaVersion = JavaVersion.VERSION_17

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    withSourcesJar()
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    testImplementation platform("org.junit:junit-bom:5.11.4")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

test{
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

tasks.withType(JavaCompile) {
    // Configure string concat to always inline compile
    options.compilerArgs.add '-XDstringConcat=inline'
}

publishing {
    publications {
        robotCommon(MavenPublication) {
            groupId = 'flingBotTech'
            artifactId = 'botsnax'
            version = '2025.0.2'

            pom {
                name = 'Botsnax'
                description = 'A library built on wpilib for programming FRC robots'
            }

            from components.java
        }
    }

    repositories {
        maven {
            name = 'buildMaven'
            url = layout.buildDirectory.dir("repo")
        }

        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/flingbottech/botsnax")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
